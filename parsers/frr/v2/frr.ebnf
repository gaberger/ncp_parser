(*This is the EBNF Grammar for FRR router*)

device = frrhead hostname? service? interfaces router-id? router? vnc? line?

(* frr *)
(* ignoring unless see a reason why *)
<frrhead> = (<'frr'> frr)*
<frr> = version | (defaults_traditional | defaults_datacenter)
<version> = <'version'> frr-version
<frr-version> = <#"\d+\.\d+\.\d+">
<defaults_traditional> = <'defaults traditional'>
<defaults_datacenter> = <'defaults datacenter'>


service = (<'service'> serviceoptions)*
<serviceoptions> = 'integrated-vtysh-config'

hostname = <'hostname'> word

(* interface section *)
interfaces = interface*
interface = <'interface'> name  description?  ip_address?
description = <'description'> (<doublequote> word+ <doublequote>)
interface-name = <'interface'> word
name = word
ip_address = <'ip address'> prefix


(* router *)
<router> = router_bgp

(* router bgp *)
router_bgp = routerbgp bgplist? neighbors afiu
<routerbgp> = <'router bgp'> asn
asn = number

bgplist = (<'bgp'> bgp)*
bgp = medoptions | bestpath | confederation

router-id = <'router-id'> address
<medoptions> = always-compare-med | deterministic-med
always-compare-med = 'always-compare-med'
deterministic-med =  'deterministic-med'

bestpath = (<'bestpath'> bestpathoptions)*
<bestpathoptions> = compare-routerid | as-path_confed
compare-routerid = 'compare-routerid'
as-path_confed = 'as-path confed'

confederation = (<'confederation'> confederationoptions)*
<confederationoptions> = identifier | peers
peers = <'peers'> number+
identifier = <'identifier'> number



(* neighbors *)

neighbors = (<'neighbor'> neighbor)*
neighbor = naddr | npeer
npeer = word npeeroptions
<npeeroptions> = 'peer-group'
naddr = (address neighboroptions)*
<neighboroptions> = neighborattrs | neighborvalues
<neighborvalues> = next-hop-self
next-hop-self = 'next-hop-self'
<neighborattrs> = remote-as | peer-group | update-source | send-community | advertisement-interval | route-map
remote-as = <'remote-as'> number
peer-group = <'peer-group'> word
send-community = <'send-community'> word
update-source = <'update-source'> address
advertisement-interval = <'advertisement-interval'> number
route-map = (<'route-map'> map)
map = (routemap direction)*
<routemap> = word

(* address-family *)
afiu = (address-family  afneighbors* exit-address-family)*
address-family = (<'address-family'> afoptions)
<afoptions> = 'ipv4 unicast' | 'ipv6 unicast'
afneighbors =  afneighbor*
afneighbor = (<'neighbor'> afnpeer)
afnpeer = (word noptions)
<noptions> = addpath-tx-all-paths / soft-reconfiguration_inbound
<addpath-tx-all-paths> = 'addpath-tx-all-paths'
<soft-reconfiguration_inbound> = 'soft-reconfiguration inbound'
exit-address-family = 'exit-address-family'


(* vnc *)
vnc = <'vnc'> vncdefaults vncoptions exitvnc
vncdefaults = 'defaults'
<vncoptions> = response-lifetime
response-lifetime = <'response-lifetime'> number
exitvnc = 'exit-vnc'


(* line *)
line = <'line'> 'vty'

(* Primitives *)
<direction> = in | out
<in> = 'in'
<out> = 'out'
<bool> = 'no'
no = <'no'>
le = <'le'> number
gt = <'gt'> number
<community-type> = number #":" number
<permit> = 'permit'
<deny> = 'deny'
<ipv4address> = address
<address> = #"\d+\.\d+\.\d+\.\d+"
<prefix> = cidr
<cidr> = (address '/' number)
<number> = #"[0-9]+"
<token> = word | number
<word> = #'[a-zA-Z0-9()\\.,-^?_|]+'
<number> = #'[0-9]+'
<doublequote> = #"\""
